apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.agenthun.eseal"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 7
        versionName "1.2.5"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    //签名
    signingConfigs {
        debug {
        }
        relealse {
            storeFile file("/Users/agenthun/Documents/AndroidStudioSave/hun_release.jks")
            storePassword "agenthun"
            keyAlias "agenthun"
            keyPassword "agenthun"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true

            // 移除无用的resource文件
            shrinkResources false
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.relealse
        }
    }

    //修改文件名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                File outputDirectory = new File(outputFile.parent);
                def fileName
                if (variant.buildType.name == "release") {
                    fileName = "${getAppName()}_v_${formateVersionName(defaultConfig.versionName)}_${packageTime()}.apk"
                } else if (variant.buildType.name == "debug") {
                    fileName = "${getAppName()}_v_${formateVersionName(defaultConfig.versionName)}_${packageTime()}-${getGitBranchName()}-debug.apk"
                }
                output.outputFile = new File(outputDirectory, fileName)
            }
        }
    }

    repositories {
        maven { url 'http://jsoup.org' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    }
    productFlavors {
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

def getAppName() {
    return "ESeal";
}

def formateVersionName(String versionName) {
    return versionName.replaceAll("\\.", "_");
}

def packageTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def getGitBranchName() {
    return 'git symbolic-ref --short -q HEAD'.execute().text.trim()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'org.jsoup:jsoup:1.10.1'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.arimorty:floatingsearchview:2.0.3'
    compile 'com.github.gabrielemariotti.recyclerview:recyclerview-animators:0.3.0-SNAPSHOT@aar'
    compile 'com.commonsware.cwac:camera:0.6.+'
    compile 'com.ramotion.foldingcell:folding-cell:1.1.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.google.android.gms:play-services-maps:10.0.1'
    compile 'com.google.maps:google-maps-services:0.1.17'
    compile project(':versionupdate')
}
